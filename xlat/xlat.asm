; vim: ft=nasm

section .data

  StatMsg: db "Processing...", 10
  StatLen: equ $ - StatMsg
  DoneMsg: db "...done!", 10
  DoneLen: equ $ - DoneMsg

; translation table converts:
;   - lowercase chars to uppercase
;   - non-printable chars to spaces (except LF and HT)
Upcase:
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,09h,0Ah,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,21h,22h,23h,24h,25h,26h,27h,28h,29h,2Ah,2Bh,2Ch,2Dh,2Eh,2Fh
  db 30h,31h,32h,33h,34h,35h,36h,37h,38h,39h,3Ah,3Bh,3Ch,3Dh,3Eh,3Fh
  db 40h,41h,42h,43h,44h,45h,46h,47h,48h,49h,4Ah,4Bh,4Ch,4Dh,4Eh,4Fh
  db 50h,51h,52h,53h,54h,55h,56h,57h,58h,59h,5Ah,5Bh,5Ch,5Dh,5Eh,5Fh
  db 60h,41h,42h,43h,44h,45h,46h,47h,48h,49h,4Ah,4Bh,4Ch,4Dh,4Eh,4Fh
  db 50h,51h,52h,53h,54h,55h,56h,57h,58h,59h,5Ah,7Bh,7Ch,7Dh,7Eh,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h

; translation table converts:
;   - non-printable chars to spaces (except LF and HT)
NonPrintToSpace:
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,09h,0Ah,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,21h,22h,23h,24h,25h,26h,27h,28h,29h,2Ah,2Bh,2Ch,2Dh,2Eh,2Fh
  db 30h,31h,32h,33h,34h,35h,36h,37h,38h,39h,3Ah,3Bh,3Ch,3Dh,3Eh,3Fh
  db 40h,41h,42h,43h,44h,45h,46h,47h,48h,49h,4Ah,4Bh,4Ch,4Dh,4Eh,4Fh
  db 50h,51h,52h,53h,54h,55h,56h,57h,58h,59h,5Ah,5Bh,5Ch,5Dh,5Eh,5Fh
  db 60h,61h,62h,63h,64h,65h,66h,67h,68h,69h,6Ah,6Bh,6Ch,6Dh,6Eh,6Fh
  db 70h,71h,72h,73h,74h,75h,76h,77h,78h,79h,7Ah,7Bh,7Ch,7Dh,7Eh,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h
  db 20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h,20h

section .bss

  READLEN equ 1024
  ReadBuffer: resb READLEN

section .text

global _start

_start:
  nop

; Display "Processing..." on stderr
  mov eax, 4          ; sys_write
  mov ebx, 2          ; stderr
  mov ecx, StatMsg    ; message
  mov edx, StatLen    ; message length
  int 80h

; Fill buffer from stdin
read:
  mov eax, 3          ; sys_read
  mov ebx, 0          ; stdin
  mov ecx, ReadBuffer
  mov edx, READLEN
  int 80h

  mov ebp, eax        ; save sys_read return value for later
  cmp eax, 0          ; EOF?
  je done

; setup registers for translate step
  mov ebx, Upcase     ; load translation table
  mov edx, ReadBuffer ; data to translate
  mov ecx, ebp        ; number of bytes read that we saved earlier

; translate data in the buffer
translate:                  ; do
  mov al, byte [edx + ecx]  ;   load char into AL for translation
  xlat                      ;   translate char in AL via xlat
  mov byte [edx + ecx], al  ;   put translated char back into buffer
  sub ecx, 1                ;   move backwards through the buffer (dec doesn't affect CF)
  jnc translate             ; while (ecx >= 0)

; write translated text
write:
  mov eax, 4            ; sys_write
  mov ebx, 1            ; stdout
  mov ecx, ReadBuffer
  mov edx, ebp
  int 80h

  jmp read              ; read and process another buffer

; "I'm done" on stderr
done:
  mov eax, 4            ; sys_write
  mov ebx, 2            ; stderr
  mov ecx, DoneMsg
  mov edx, DoneLen
  int 80h

  mov eax, 1      ; exit with code zero
  mov ebx, 0
  int 80H
